//======================================================================
// モーション定義ファイル（ベルゼバブ）
//======================================================================

#CharMotTable:0160	 	//***をキャラ番号（ファイル番号そのまま）に置き換える
en_160m.it3				//モーションファイル指定


//・空中戦：飛び道具ｘ２
//　↓
//（叩き落すor一定時間経過）
//　↓
//・地上戦：飛び道具ｘ１、自爆攻撃


////////////////////////////////////////////////////////////////////////
//ＳＥ再生

if_LEN 1500 0
{
	OTO 42	0500	 603 0 0 0	//浮上
	OTO 41	0437	 605 0 0 0	//落下
	OTO 31	0053	 728 0 0 0	//電気ショック
	OTO 31	0053	 329 0 0 0	//電気ショック
	OTO 39	0375	 941 0 0 0	//カマイタチ両手
	OTO 39	0375	 399 0 0 0	//カマイタチ両手
	OTO 47	0695	 941 0 0 0	//地上カマイタチ
	OTO 47	0695	 399 0 0 0	//地上カマイタチ
	OTO 48	0748	 941 0 0 0	//地上カマイタチ
	OTO 48	0748	 399 0 0 0	//地上カマイタチ
	OTO 46	0636	 605 0 0 0	//自爆
	OTO 46	0636	 482 0 0 0	//自爆
	OTO 46	0636	 421 0 0 0	//自爆
}


////////////////////////////////////////////////////////////////////////
// モーションセット定義

	//------------------------------------------------------------------
	// 登場
	//------------------------------------------------------------------
	SET 80 80 0575 0598 -1 00	//登場

	//------------------------------------------------------------------
	// 待機
	//------------------------------------------------------------------
	SET 02 02 0575 0598 -1 00	//地上待機

	//------------------------------------------------------------------
	// 歩行
	//------------------------------------------------------------------
	SET 00 10 0785 0792 -1 00	//歩行（開始）
	SET 10 10 0793 0816 -1 00	//歩行
		E_charbase0		10 0793 30018 24 0 0		//＠胴体アタリ

	//------------------------------------------------------------------
	// 上昇
	//------------------------------------------------------------------
	SET 42 43 0496 0514 -1 00	//空中へ（開始）
	SET 43 -1 0515 0544 01 05	//空中へ

	//------------------------------------------------------------------
	// 飛行
	//------------------------------------------------------------------
	SET 01 01 0001 0019 -1 00	//飛行

	//------------------------------------------------------------------
	// 落下
	//------------------------------------------------------------------
	SET 40 -1 0415 0435 -1 00	//やられ落下（開始→着地するまで固定）
	SET 41 -1 0436 0495 00 05	//やられ落下（着地〜立て直し）
		E_charbase0		41 0436 1170 150 0 0		//＠汎用バウンド
		E_charbase0		41 0436 2105 100 0 0		//＠振動（小）

	//------------------------------------------------------------------
	// 攻撃（急降下アタック）
	//------------------------------------------------------------------
	SET 32 33 0085 0125 -1 00	//急降下アタック（開始）
	SET 33 34 0126 0135 -1 00	//急降下アタック（空中待機ループ）
	SET 34 35 0136 0169 -1 00	//急降下アタック03
//	SET 35 -1 0170 0194 02 10	//急降下アタック04
	SET 35 -1 0170 0182 -1 00	//急降下アタック04
		E_charbase0		35 0180  1940 150 0  0		//＠衝撃波
		E_charbase0		35 0182 31040  15 0 20		//＠衝撃波アタリ
		E_charbase0		35 0180  2106 100 0  0		//＠振動（大）
	
	//※未使用（用途不明）
	SET 49 01 0195 0244 -1 00	//急降下アタック降りモーション

	//------------------------------------------------------------------
	// 攻撃（電気ショック）
	//------------------------------------------------------------------
	SET 30 31 0021 0051 -1 00	//電気ショック（開始）
	SET 31 01 0052 0079 -1 00	//電気ショック
		E_Bone_kao		31 0052 1280 100 0 0 ADL		//＠電気
		E_Bone_kao		31 0056 1280 100 0 0 ADL		//＠電気
		E_Bone_kao		31 0060 1280 100 0 0 ADL		//＠電気
		E_Bone_kao		31 0064 1280 100 0 0 ADL		//＠電気
		E_Bone_kao		31 0068 1280 100 0 0 ADL		//＠電気

	//------------------------------------------------------------------
	// 攻撃（カマイタチ）	※割愛（両手verに統合）
	//------------------------------------------------------------------
//	SET 36 37 0250 0297 -1 00	//カマイタチ左手
//		E_Bone_udeR		36 0270 1550 100 0 0		//＠衝撃波
//	SET 37 01 0298 0344 -1 00	//カマイタチ右手（３連撃を行う際はこの37番は再生せず36→38とつなぐ。）
//		E_Bone_udeL		37 0298 1550 100 0 0		//＠衝撃波

	//------------------------------------------------------------------
	// 攻撃（カマイタチ両手）
	//------------------------------------------------------------------
	SET 38 39 0350 0362 -1 00	//カマイタチ両手（開始）
	SET 39 01 0363 0408 -1 00	//カマイタチ両手（終了）
		E_Bone_udeR		39 0375 1550 100 0 0 ADL		//＠衝撃波
		E_Bone_udeL		39 0375 1550 100 0 0 ADL		//＠衝撃波

	//------------------------------------------------------------------
	// 攻撃（自爆）
	//------------------------------------------------------------------
	SET 45 46 0605 0635 -1 00	//防御壁（開始）
	SET 46 00 0636 0668 -1 00	//防御壁
		E_charbase0		46 0636 2270 100 0 0		//＠爆発

	//------------------------------------------------------------------
	// 攻撃（地上カマイタチ）
	//------------------------------------------------------------------
	SET 47 -1 0675 0724 00 03	//カマイタチ左手（地上）
		E_Bone_udeL		47 0695 1550 100 0 0		//＠衝撃波

	SET 48 -1 0730 0779 00 03	//カマイタチ右手（地上）
		E_Bone_udeR		48 0748 1550 100 0 0		//＠衝撃波




////////////////////////////////////////////////////////////////////////
// ルーチン分岐

	//mF0 = 状態（0:開幕待機/1:飛行状態/2:墜落後）
	//mF2 = 飛行中に行動した回数
	//mF3 = 浮上維持のための変数
	//mF4 = ダメージ喰らった回数（＝自爆攻撃発動までのカウントダウン用）

	if_INIT
	{
//		B_NEXT -1 0 0 0 0 00	//DEBUG:地上アルゴテスト
	}

	//------------------------------------------------------------------
	// 登場
	//------------------------------------------------------------------
	//     set deg tim rnd  len next
	B_NEXT  80   0  60   0    0   02	//⇒始動

	//------------------------------------------------------------------
	// 待機
	//------------------------------------------------------------------
	//    set typ spd rot xx1 xx2
	B_MOV  02   0   0  16  00  00
	
	//★開幕と同時に浮上
	if_SET 02
	{
		if_mF0= 0
		{
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙

			mF0_add -1 -1 1
			
			//     set deg tim rnd len next
			B_NEXT  -1   0   0   0   0   42		//⇒上昇
		}
	}

	//------------------------------------------------------------------
	// 歩行
	//------------------------------------------------------------------
	//    set typ spd rot xx1 xx2
	B_MOV  00   0   0  16  00  00
	B_MOV  10   0   6  16  00  00
	
		if_mF4> 10
		{
			//     set deg tim rnd len next
			B_NEXT  10   0   0   0 550   45		//⇒自爆
		}
		//     set deg tim rnd len next
		B_NEXT  10   0  90  60   0   47		//⇒地上カマイタチ（左）
		B_NEXT  10   0  90  60   0   48		//⇒地上カマイタチ（右）
		B_NEXT  10   0 180   0   0   47		//⇒地上カマイタチ（左）

	//★ダメージ回数カウント（地上時）
	if_SET 00 10	47 48
	{
		if_EV DAMAGE
		{
			mF4_add -1 -1 1
		}
	}

	//★ダメージ回数リセット
	if_SET 45
	{
		if_TIME 0 0
		{
			mF4_set -1 -1 0
		}
	}

	if_SET 00 10
	{
		if_time 15
		{
			OTO -1	-1	 441 0 0 0
			OTO -1	-1	 302 0 0 0
		}
	}

	//------------------------------------------------------------------
	// 攻撃（地上カマイタチ）
	//------------------------------------------------------------------
	//    set typ spd rot xx1 xx2
	B_MOV  47   0   0  16   8  00		//左
	B_MOV  48   0   0  16  -8  00		//右



	//------------------------------------------------------------------
	// 飛行
	//------------------------------------------------------------------
	//    set typ spd rot xx1 xx2
	B_MOV  01   0   5  16  00  00

		//★一定時間経ったら自分から地上に降りるようにする
		if_mF2= 15
		{
			mF0_add -1 -1 1

			//     set deg tim rnd len next
			B_NEXT  01   0   0   0   0   32		//⇒急降下アタック
		}

		//     set deg tim rnd  len next
		B_NEXT  01   0  90   5  800   38		//⇒カマイタチ（近距離でも稀に撃つ）
		B_NEXT  01   0  90   0  800   30		//⇒電気ショック
		B_NEXT  01   0  90   0    0   38		//⇒カマイタチ
	
	//飛行中の攻撃回数カウント
	if_SET 30 38
	{
		if_TIME 0 0
		{
			mF2_add -1 -1 1
		}
	}

	//★浮上開始
	if_SET 43
	{
		MOV -1 0515 0544 0 0 30
	}

	//★重力に逆らい続けることで「飛行」を実現
	if_SET 01	30 31	38 39
	{
//		MOV -1 0001 0019 0 0 1
		
		//※上の方法だと徐々に上昇してしまうので、トリッキーな方法で対処
		if_mF3= 1
		{
			mF3_set -1 -1 0
			
			MOV -1 -1 -1 0 0  1
		}
		else
		{
			mF3_set -1 -1 1

			MOV -1 -1 -1 0 0 -1
		}
	}

	//★一定ダメージで落下
	if_EV DAMAGE
	{
		if_HP 60 0
		{
			if_mF0= 1
			{
				E_charbase0			-1 -1 2145 100 0 0		//＠フラッシュ

				mF0_add -1 -1 1

				F_RESET -1 -1 NO_AIR	//★浮かし属性を一時的にON

				JMP -1 -1 150 0 0 0		//★ちょっと浮かす（m_zimen=0にするため）

				//     set deg tim rnd len next
				B_NEXT  -1   0   0   0   0   40		//⇒落下
			}
		}
	}

	//浮上／落下中は当たりなし
	if_SET 42 43	40 41
	{
		H_@csp -1 -1 11 1 0		//無敵ON
	}
	else
	{
		H_@csp -1  1  0 3 0		//無敵OFF
	}



	//------------------------------------------------------------------
	// 落下
	//------------------------------------------------------------------
	//    set typ spd rot xx1 xx2
	B_MOV  40   0   0   0  00  00

	if_SET 40
	{
		if_ZIMEN
		{
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙
			E_charbase0		-1 -1  570 265 0 0 		//＠土煙

			E_charbase0		-1 -1 2108 100 0 0		//＠振動（大ｘ２）

			//     set deg tim rnd len next
			B_NEXT  -1   0   0   0   0   41 -1		//⇒接地（補間なしで繋ぐ）

			F_SET -1 -1 NO_AIR		//★浮かし属性を再びOFF
		}
	}


	//------------------------------------------------------------------
	// 攻撃（急降下アタック）
	//------------------------------------------------------------------
	if_SET 32 33 34 35
	{
		//    set typ spd rot xx1 xx2
		B_MOV  -1   0  12   8  00  00
	}

	if_SET 32
	{
		//とりあえず重力に逆らい続けとく
//		MOV -1 0085 0125 0 0 1
		MOV -1 0085 0099 0 0 1
		
		//途中でジャンプに切り替えます
		JMP 32 0100 2500 0 0 0
	}

	if_SET 35
	{
		if_ZIMEN
		{
			//     set deg tim rnd len next
			B_NEXT  -1   0   0   0   0   00 03		//⇒接地
		}
	}


	//------------------------------------------------------------------
	// 攻撃（電気ショック）
	//------------------------------------------------------------------
	if_SET 30 31
	{
		//    set typ spd rot xx1 xx2
		B_MOV  -1   0   5  16  00  00
	}

	//------------------------------------------------------------------
	// 攻撃（カマイタチ両手）
	//------------------------------------------------------------------
	if_SET 38 39
	{
		//    set typ spd rot xx1 xx2
		B_MOV  -1   0   0  16  00  00
	}
	


#EndCharMotTable

