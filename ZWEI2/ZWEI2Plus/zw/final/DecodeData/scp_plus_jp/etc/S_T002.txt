//──────────────────────────────
//	name("●ポックル")
//──────────────────────────────
/////////////////////////////////////////////////////////////////////////////////////////////////////
#ScriptTable

TK_POCKLE
{
	TK_begin()
////////////////////////////////////////////////////////////////
//仲間全員が話し相手に振り向く
////////////////////////////////////////////////////////////////
	KAO(POCKLE, "1", "1", "1")	
	KAO(PLAYER1, "1", "1", "1")	
	rot_chr(PLAYER1,30,POCKLE)
//───────────────
//⑪ルゥが居る
//───────────────
	if(F5_00_HearLunar)
	{
		KAO(PET, "1", "1", "1")	
		rot_chr(PET,30,POCKLE)
		
		//オデッサがいる
		if(F5_08_JoinOdessa && !F5_13_KillDygald)
		{
			KAO(PLAYER2, "1", "1", "1")	
			rot_chr(PLAYER2,30,POCKLE)
		}
		//アルウェンがいる
		else
		if(F6_00_GoToMeru)
		{
			KAO(PLAYER2, "1", "1", "1")	
			rot_chr(PLAYER2,30,POCKLE)
		}
	}
//───────────────
//⑦一人で行動
//───────────────
	else
	if(F4_00_StartSolo)
	{
	//───────────────
	//スバルが居る
	//───────────────
		if(F4_02_GoVillage2 && !F4_06_StopAttack)
		{
			KAO(PLAYER2, "1", "1", "1")	
			rot_chr(PLAYER2,30,POCKLE)
		}
		else
	//───────────────
	//アルウェンが居る
	//───────────────
		if(F4_09_GoShrineMia)
		{
			KAO(PLAYER2, "1", "1", "1")	
			rot_chr(PLAYER2,30,POCKLE)
		}
	}
//───────────────
//～⑥アルウェンが居る
//───────────────
	else
	if(!F4_00_StartSolo)
	{
		KAO(PLAYER2, "1", "1", "1")	
		rot_chr(PLAYER2,30,POCKLE)
	}
//会話処理へ
////////////////////////////////////////////////////////////////
	
	//──────────────────────────────
	//▼⑫飛行機でメルセデクに乗り込むことにした（ラグナ・アルウェン・ルゥ）
	//──────────────────────────────
	//▼フラグ⑫（メルセデクに乗り込む）
	//　　ピピロと共に飛行場のポックル機の前にいる。
	//	name("★初回（ピピロと共通／１回のみ／イベント処理）")
	//　　話しかけるとキャラ再配置してイベント発生。
	//　　ピピロと共通なため、あちらで起こしたら発生しない。
	//──────────────────────────────
	if( F6_00_GoToMeru )
	{	
		if(!FM_12_TalkPocPip)
		{
			//ピピロと共通初回イベント
			EV("TALK_POCPIP_12")
			wait_EV("TALK_POCPIP_12")
		}
		else
		{
		//	name("★以後、繰り返し（飛行機を見続けている？）")
			MES(POCKLE,"（この飛行機に乗って\n  巨大竜巻に挑むのか……）",0)
			MES(POCKLE,"（うーん、ちょっと\n  ドキドキしてきちゃった。）",0)
			MES(POCKLE,"（ピピロと一緒に《幻の大地》に\n  挑んだ時以来かも……）",0)
			MES_close(POCKLE)
		}
	}
	else
	//──────────────────────────────
	//▼⑪クリスタルバレーを起動することにした（ラグナ・ルゥ（・オデッサ））
	//──────────────────────────────
	if(F5_00_HearLunar)
	{
		//──────────────────────────────
		//▽クリスタルバレーを起動した
		//　　クリスタルバレーでピピロたちを見守っている。
		//─────────────────────
		if( F5_06_RunCrystal )
		{
			rot_chr(POCKLE,30,PLAYER1)

			//会話用モーション
			MOT_SET(POCKLE,151,-1,434,434,-1,0)		//通常
			MOT_SET(POCKLE,152,-1,407,407,-1,0)		//あ、オデッサさん！
			
		//	name("⇒ダイガルドを倒した")
		//　　ピピロの近くに寄っている状態。
			if(F5_13_KillDygald)
			{
				MOT(POCKLE,151,10)
				MES(POCKLE,"そろそろピピロたちも\n辛くなってきたみたいです……",0)
				MES(POCKLE,"ラグナさん、頑張ってください！",0)
				MES_close(POCKLE)
			}
		//	name("⇒ダイガルドを倒していない")
			else
			if(F5_08_JoinOdessa)
			{
			//	name("★１回目")
				if(!F_POCKLE)
				{
					MOT(POCKLE,152,10)
					rot_chr(POCKLE,30,PARTNER)
					MES(POCKLE,"あ、オデッサさん！",0)
					MES_close(POCKLE)

					rot_chr(PARTNER,30,POCKLE)
				//	name2(PARTNER)
					MES(PARTNER,"やあ、ポックルの坊や。",0)
					MES(PARTNER,"私はラグナに付き合うから、\nあんた達はこの場を任せたよ。",0)
					MES_close(PARTNER)

				//	name2(POCKLE)
					MES(POCKLE,"は、はい……！",0)
					MES(POCKLE,"（はあ、相変わらず\n  カッコイイお姉さんだなぁ㈱）",0)
					MES_close(POCKLE)
					
					F_set(POCKLE)
				}
				else
				{
					MOT(POCKLE,151,10)
				//	name("★以後、繰り返し")
					MES(POCKLE,"ラグナさん、オデッサさん！",0)
					MES(POCKLE,"２人とも頑張ってくださいね！",0)
					MES_close(POCKLE)
				}
			}
			else
			{
				MOT(POCKLE,151,10)
		//	name("⇒オデッサが仲間になっていない")
				MES(POCKLE,"もしモンスターが現れても\n僕たちで何とかします！",0)
				MES(POCKLE,"ラグナさん、気をつけて！",0)
				MES_close(POCKLE)
			}
			MOT(POCKLE,150,10)
		}
		//──────────────────────────────
		//⑪▽ポックルたちに事情を話した
		//　　ピピロと共にクリスタルバレーの一角にいる。
		//	name("★初回（ピピロと共通／１回のみ／イベント処理）")
		//　　話しかけるとキャラ再配置してイベント発生。
		//　　ピピロと共通なので、あちらで起こすと発生しない。
		//──────────────────────────────
		else
		if(!F5_04_OfferPipiro)
		{
			//ピピロとの共通会話
			EV("EV_05_04_OfferPipiro")
			wait_EV("EV_05_04_OfferPipiro")
		}
		//──────────────────────────────
		//⑪説得後
		//　　ピピロと共にクリスタルバレーの一角にいる。
		//──────────────────────────────
		else
		{
			if(!FM_11_TalkPocPip)
			{
				//	name("★初回（ピピロと共通／１回のみ／イベント処理）")
				//　　話しかけるとキャラ再配置してイベント発生。
				//　　ピピロと共通なので、あちらで起こすと発生しない。
				EV("TALK_POCPIP_11")
				wait_EV("TALK_POCPIP_11")
				
			}
			else
			{
				MOT(POCKLE,2,10)
				rot_chr(POCKLE,30,PLAYER1)
			//	name("★以後、繰り返し")
				MES(POCKLE,"うーん、このアクセサリ、\nそろそろ止めた方がいいのかなぁ。",0)
				MES(POCKLE,"でも、これとニコバッジは\n僕のトレードマークな気がするし……",0)
				MES_close(POCKLE)
				rot(POCKLE,30,0)
				MOT(POCKLE,150,10)
			}
		}
	}
	else
	//──────────────────────────────
	//▼⑦ラグナは単独行動を始めた　▽スバルが仲間になった
	//──────────────────────────────
	//　　村のペットショップにピピロと共にいる。
	//　　置くにある棚の前で品揃えを見ているような感じ。
	//	name("★初回（ピピロと共通／一回のみ／イベント処理）")
	//　　話しかけるとキャラ再配置してイベント発生。
	//　　ピピロと共通なので、あちらで起こしたら発生しない。
	//──────────────────────────────
	if( F4_02_GoVillage2 )
	{
		if(!FM_07_TalkPocPip)
		{
			EV("TALK_POCPIP_07")
			wait_EV("TALK_POCPIP_07")
		}
		else
		{
		//	name("★以後、繰り返し")
			MES(POCKLE,"ペットフードか……\nどのタイプがいいかなぁ。",0)
			MES(POCKLE,"肉を使ったタイプか\n魚を使ったタイプか……",0)
			MES(POCKLE,"わっ、大豆を使った\nヘルシーなタイプまであるんだ。",0)
			MES_close(POCKLE)
		}
	}
	else
	//──────────────────────────────
	//▼⑥ロアルタ村の人々を救出し、ムーンブリア城へ向かった（マップ：夜）
	//──────────────────────────────
	if( F3_00_GoMoonCastle )
	{
		//──────────────────────────────
		//▽城の玄関広間に入った
		//　　ロアルタ村の広場にいる。
		//　　倒れた村人を介抱しているような動作。
		//──────────────────────────────
		if(F3_02_InMoonCastle)
		{
			MES(POCKLE,"よかった……\n顔色が良くなってきた。",0)
			MES(POCKLE,"このままゆっくりと\n休ませてあげられたら……",0)
			MES_close(POCKLE)
		}
		else
		//──────────────────────────────
		//▼フラグ⑥（夜・ロアルタ村襲撃直後）
		//　　ロアルタ村の広場にいる。
		//　　倒れた村人を介抱しているような動作。
		//──────────────────────────────
		if(!FM_06_TalkPockle)
		{
		//	name("★初回（１回のみ／イベント処理）")
			MES(POCKLE,"ダメだ……\n完全にぐったりしてる。",0)
			MES(POCKLE,"それにしても……\nこんな風に血を吸って\n人を操るなんて……",0)
			MES_close(POCKLE)

		//　　ポックル、アルウェンの方に向き直る。
			MOT(POCKLE,2,15)
			rot_chr(POCKLE,30,PARTNER)
			wait(10)
			EMO(POCKLE,EMO_TENTEN)
			wait(30)
			
			rot_chr(PARTNER,30,POCKLE)
			MES(PARTNER,"ふむ……私が恐いか？",0)
			MES(PARTNER,"確かに私も\n同じようなことは出来るぞ。",0)
			MES(PARTNER,"あまり優雅ではないから\n今まで試したことはないがな。",0)
			MES_close(PARTNER)

		//	name2(POCKLE)
			MES(POCKLE,"い、いえっ！",0)
			MES(POCKLE,"アルウェンさんは\n絶対にそんな事をしないって\n僕、信じていますから！",0)
			MES_close(POCKLE)

		//	name2(PARTNER)
			MES(PARTNER,"ほう……？",0)
			MES_close(PARTNER)

		//	name2(PIPIRO)
			rot_chr(PIPIRO,30,POCKLE)
			MES(PIPIRO,"フン、綺麗なお姉さんが\nそんな事をするはずがないって\nそれだけの理由でしょーが。",0)
			MES_close(PIPIRO)

			rot_chr(POCKLE,30,PIPIRO)
		//	name2(POCKLE)
			MES(POCKLE,"ま、まあそうだけど……",0)
			MES_close(POCKLE)

		//	name2(RAGNA)
			MES(RAGNA,"やれやれ。",0)
			MES_close(RAGNA)

		//	name2(PARTNER)
			MES(PARTNER,"ふふ、そなたに感謝を。",0)
			MES_close(PARTNER)
			
			rot(POCKLE,30,120)
			rot(PIPIRO,30,220)
			MOT(POCKLE,150,15)
			F_set(FM_06_TalkPockle)
		}
	//	name("★以後、繰り返し")
		else
		{
			look_chr( POCKLE, PLAYER1,"Bone_Head")
			MES(POCKLE,"村のみんなの介抱は\n僕たちに任せてください。",0)
			MES(POCKLE,"お２人は急いでお城へ！",0)
			MES_close(POCKLE)
			look_off(POCKLE)
		}
	}
	else
	//──────────────────────────────
	//▽大鉄塔でポックルたちと会った
	//──────────────────────────────
	//　　大鉄塔中継、炎の扉近くピピロと共にいる。
	if( F1_22_EndFirst && !F2_17_EndSecond )
	{
		rot_chr(POCKLE,30,PLAYER1)
	//	name("★左右のどちらも解除していない")
		if(!F100 && !F101)
		{
			MES(POCKLE,"えっと……\nどうするつもりですか？",0)
			MES_close(POCKLE)
		}
	//	name("★左右のどちらかを解除した")
		else
		{
			MES(POCKLE,"へっ……",0)
			MES_close(POCKLE)
		}
		rot(POCKLE,30,80)//手動で入れても向きが戻らない……
	}
	else
	//──────────────────────────────
	//▼②ロアルタ村へ向かうことにした
	//──────────────────────────────
	//▽神殿前でポックルたちと会った
	//　　ピピロと共に酒場のテーブルに座っている。
	//　　（向かい合わせではなく、隣り合わせの席配置で）
	//　　ピピロの前にはイチゴ杏仁パフェと抹茶シフォンケーキ、
	//　　ポックルの前には日本茶とセンベイなどが置かれている。
	//──────────────────────────────
	if( F1_15_GoVillage )
	{
	//	2回目のイベントまではピピロと共通イベント
		if(!FM_02_TalkPocPip_1)
		{
			EV("TALK_POCPIP_02_1")
			wait_EV("TALK_POCPIP_02_1")
		}
		else
		if(!FM_02_TalkPocPip_2)
		{
			EV("TALK_POCPIP_02_2")
			wait_EV("TALK_POCPIP_02_2")
		}
	//	name("★以後、繰り返し")
		else
		{
			look_chr( POCKLE, PLAYER1,"Bone_Head")

			MES(POCKLE,"うーん、何だか\n自信が付いちゃった㈱",0)
			MES(POCKLE,"よし、これからは\n１日１０ギャグくらいを\n目標にしようっと♪",0)
			MES_close(POCKLE)

			look_chr( PIPIRO, POCKLE,"Bone_Head")
		//	name2(PIPIRO)
			MES(PIPIRO,"調子に乗るな、コラ。",0)
			MES_close(PIPIRO)
			look_off(PIPIRO)
			look_off(POCKLE)
		}
	}
	
	TK_end()
}

#EndScriptTable
/////////////////////////////////////////////////////////////////////////////////////////////////////
